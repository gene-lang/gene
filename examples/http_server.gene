#!/usr/bin/env gene run

# Import all members from genex/http to current namespace
# (import genex/http/*)

# The main app class
(class App

   # Constructor
   # First arg is automatically stored as `port` property in the instance
   (ctor port
      (/port = port)
      # Initializes `middleware` property as an empty array
      (/middlewares = [])
   )

   # Helper method for constructing handlers from /middlewares and /handler
   (method construct_handler middleware_index
      (if (middleware_index < (self/middlewares .size))
         (var middleware (/middlewares .get middleware_index))
         (var handler    (.construct_handler (middleware_index + 1)))
         (middleware handler)
       else
         self/handler
      )
   )

   # Construct the root handler based on /middlewares and /handler
   # Start the HTTP server
   (method start _
      # Note: print and println inserts " " between arguments
      (println "Starting HTTP server at port" self/port "...")

      (var root_handler (.construct_handler 0))
      (start_server /port root_handler)
      (run_forever)
   )
)

# A dummy router that calls handlers one by one until one responds the request.
(class DummyRouter

   # Constructor
   (ctor _
      (/handlers = [])
   )

   # Define `call` method which enables the router to be invoked like any handler,
   # i.e. (<dummy router> req)
   (method call req
      (println "Router.call called with self:" self "req:" req)

      (for h in /handlers
         (var result (h req))
         (if result
          then
            (println result/status req/method req/url)
            (return result)
         )
      )

      # No handler responded the request, return 404 Not found.
      (respond 404)
   )
)

# A simple handler that responds to /hello
(fn hello [req]
   (if (req/path != "/hello")
    then
      (println "  Path doesn't match /hello, returning nil")
      (return)
    else
      (println "  Path matches /hello!")
      (var params req/params)
      (if (params .contains "name")
       then (respond #"Hello #{params/name}!")
       else (respond "Hello world!"))
   )
)

# A handler that responds to /secret
# Please note that the authentication is handled in `auth` middleware.
(fn secret [req]
   (if (req/path == "/secret")
      (respond "Sssh, do not tell this to anyone!")
   )
)

# A dummy passcode used for authentication
(var passcode "secret")

# Middlewares wrap `handler` with some logic.
# `auth` is a middleware that makes sure access to critical resources are authenticated.
(fn auth [handler]

   # (fn [arg] ...) defines an anonymous function.
   (fn [req]
      (if (req/path != "/secret")
         (return (handler req))
      )

      (var authenticated false)
      # Read and check passcode from Authorization header if it exists
      (if (req/headers .contains "authorization")
         (authenticated = ((gene/base64_decode req/headers/authorization) == passcode))
      )
      (if authenticated
         (handler req)
       else
         (respond 401)
      )
   )
)

# Server port defaults to 2080 but can be passed in the command line.
(var port 2080)

# Create and initialize the app
(var app (new App port))
(app/middlewares .add auth)
(var router (new DummyRouter))
(router/handlers .add hello)
(router/handlers .add secret)
(app/handler = router)

# Start the app
(app .start)
